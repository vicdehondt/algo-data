#lang r6rs

;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*                                                                 *-*-
;-*-*               Hash Tables (Quadratic Rehashing)                 *-*-
;-*-*                                                                 *-*-
;-*-*                       Wolfgang De Meuter                        *-*-
;-*-*                 2008 Programming Technology Lab                 *-*-
;-*-*                   Vrije Universiteit Brussel                    *-*-
;-*-*                                                                 *-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-

(library
 (dictionary)
 (export new dictionary? insert! delete! find)
 (import (rnrs base (6)))
 
 (define (rehash address j M)
   (mod (+ address j) M))
 
 (define make-assoc cons)
 (define assoc-key car)
 (define assoc-value cdr)
 
 (define dictionary-tag 'quadratic-rehashing) 
 (define (make ==? M h)
   (list dictionary-tag (make-vector M 'empty) h ==?))
 (define (storage table)
   (cadr table))
 (define (hash-function table)
   (caddr table))
 (define (equality table)
   (cadddr table))
 
 (define (new ==? M h)
   (make ==? M (lambda (x) (mod (h x) M))))
 
 (define (dictionary? any)
   (and (pair? any)
        (eq? (car any) dictionary-tag)))
 
 (define (insert! table key val)
   (define vector (storage table))
   (define M (vector-length vector))
   (define h (hash-function table))
   (define ==? (equality table))
   (let rehash-iter 
     ((address (h key))
      (odd 1))
     (let ((assoc (vector-ref vector address)))
       (cond 
         ((or (eq? assoc 'empty)
              (eq? assoc 'deleted))
          (vector-set! vector address (make-assoc key val)))
         ((==? (assoc-key assoc) key)
          (vector-set! vector address (make-assoc key val)))
         (else
          (rehash-iter (rehash address odd M) (+ odd 2))))))
   table)
 
 (define (find table key)
   (define vector (storage table))
   (define M (vector-length vector))
   (define h (hash-function table))
   (define ==? (equality table))
   (let rehash-iter
     ((address (h key))
      (odd 1))
     (let ((assoc (vector-ref vector address)))
       (cond
         ((eq? assoc 'empty)
          #f)
         ((eq? assoc 'deleted)
          (rehash-iter (rehash address odd M) (+ odd 2)))
         ((==? (assoc-key assoc) key)
          (assoc-value assoc))
         (else
          (rehash-iter (rehash address odd M) (+ odd 2)))))))
 
 (define (delete! table key)
   (define vector (storage table))
   (define M (vector-length vector))
   (define h (hash-function table))
   (define ==? (equality table))
   (let rehash-iter
     ((address (h key))
      (odd 1))
     (let ((assoc (vector-ref vector address)))
       (cond
         ((eq? assoc 'empty)
          #f)
         ((eq? assoc 'deleted)
          (rehash-iter (rehash address odd M) (+ odd 2)))
         ((==? (assoc-key assoc) key)
          (vector-set! vector address 'deleted))
         (else
          (rehash-iter (rehash address odd M) (+ odd 2))))))
   table))