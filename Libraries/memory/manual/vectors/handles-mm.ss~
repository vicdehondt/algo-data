#lang r6rs

;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*                                                                 *-*-
;-*-*            Best-Fit Memory Manager (Handle Management)          *-*-
;-*-*                                                                 *-*-
;-*-*                 Theo D'Hondt - Wolfgang De Meuter               *-*-
;-*-*               1993-2009 Programming Technology Lab              *-*-
;-*-*                   Vrije Universiteit Brussel                    *-*-
;-*-*                                                                 *-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-

(library
 (handle-manager)
 (export make-vector vector? vector-free vector-ref vector-set! vector-length)
 (import (rename (except (rnrs base (6)) vector vector? vector-length)
                 (make-vector scheme:make-vector) (vector-ref scheme:vector-ref) (vector-set! scheme:vector-set!))
         (rnrs control)
         (prefix (a-d memory manual vectors best-fit-mm) vector:))
 
 (define free-handle 0)
 (define handle-mem-size 16)
 (define handle-mem (scheme:make-vector handle-mem-size vector:null))
 
 (define handle-tag 'handle)
 
 (define (pointer handle)
   (scheme:vector-ref handle-mem handle))
 
 (define (pointer! handle ptr)
   (scheme:vector-set! handle-mem handle ptr))
 
 (define (vector handle)
   (if (vector? handle)
     (pointer (cdr handle))
     (error "invalid handle" handle)))
 
 (define (initialize-handles)
   (do ((handle-idx 1 (+ handle-idx 1)))
     ((= handle-idx handle-mem-size))
     (pointer! (- handle-idx 1) handle-idx)))
 
 (define (crunch-vectors)         ; =========> NAAMGEVING 2 x index verwarrend
   (define (move-vector source destination size)
     (define index (vector:peek (+ source 1)))
     (vector:poke! destination size)
     (vector:poke! (+ destination 1) (pointer index))
     (pointer! index (vector:make destination))
     (do ((index 2 (+ index 1)))
       ((= index size))
       (vector:poke! (+ destination index) 
                     (vector:peek (+ source index)))))
   (define next-free
     (let move-left
       ((source 0)
        (destination 0))
       (if (>= source (vector:memory-size)) 
         destination
         (let ((size (vector:peek source)))
           (cond
             ((negative? size)
              (move-vector source destination (- size))
              (move-left (- source size) (- destination size)))
             (else
              (move-left (+ source size) destination)))))))
   (define size (- vector:memory-size next-free))
   (vector:reset-free)
   (if (>= size vector:lowest-size)
     (vector:insert-free next-free size)))
 
 (define (swap-pointer handle-idx)
   (define addr (vector:address (pointer handle-idx)))
   (define size (vector:peek addr))
   (pointer! handle-idx (vector:peek (+ addr 1)))
   (vector:poke! addr (- size))
   (vector:poke! (+ addr 1) handle-idx))
 
 (define (defragment-storage)
   (do ((handle-idx 0 (+ handle-idx 1)))
     ((= handle-idx handle-mem-size))
     (if (vector:vector? (pointer handle-idx))
       (swap-pointer handle-idx)))
   (crunch-vectors))
 
 (define (make-vector size)
   (if (eq? free-handle vector:null)
     (error "handle table overflow" size)
     (let
         ((handle-idx free-handle)
          (vctr (vector:make-vector size)))
       (when (eq? vctr vector:null)
         (defragment-storage)
         (set! vctr (vector:make-vector size))
         (if (eq? vctr vector:null)
           (error "Memory overflow" size)))
       (set! free-handle (pointer free-handle))
       (pointer! handle-idx vctr)
       (cons handle-tag handle-idx))))
 
 (define (vector-free handle)
   (vector:vector-free (vector handle))
   (let 
       ((handle-idx (cdr handle)))
     (pointer! handle-idx free-handle)
     (set! free-handle handle-idx)))
 
 (define (vector? any)
   (and (pair? any)
        (eq? (car any) handle-tag)))
 
 (define (vector-ref handle index)
   (vector:vector-ref (vector handle) index))
 
 (define (vector-set! handle index value)
   (vector:vector-set! (vector handle) index value))
 
 (define (vector-length handle)
   (vector:vector-length (vector handle)))
 
 (initialize-handles))