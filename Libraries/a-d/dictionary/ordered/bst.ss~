#lang r6rs

;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*                                                                 *-*-
;-*-*           Dictionary (Binary Search Tree Implementation)        *-*-
;-*-*                                                                 *-*-
;-*-*                       Wolfgang De Meuter                        *-*-
;-*-*                 2008 Programming Technology Lab                 *-*-
;-*-*                   Vrije Universiteit Brussel                    *-*-
;-*-*                                                                 *-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-

(library
 (dictionary)
 (export new dictionary? insert! delete! find empty? full?)
 (import (rnrs base)
         (prefix (a-d tree binary-search-tree) bst:))
 
 (define make-assoc cons)
 (define assoc-key car)
 (define assoc-value cdr)
 (define (for-assoc-key proc)
   (lambda (assoc1 assoc2)
     (proc (assoc-key assoc1)
           (assoc-key assoc2))))
 
 (define (new ==? <<?)
   (bst:new 
    (for-assoc-key <<?)
    (for-assoc-key ==?)))
 
 (define (dictionary? any)
   (bst:binary-search-tree? any))
 
 (define (insert! dct key val)
   (bst:insert! dct (make-assoc key val))
   dct)
 
 (define (delete! dct key)
   (bst:delete! dct (make-assoc  key 'ignored))
   dct)
 
 (define (find dct key)
   (define assoc (bst:find dct (make-assoc key 'ignored)))
   (if assoc
     (assoc-value assoc)
     assoc))
 
 (define (empty? dct)
   (bst:empty? dct))
 
 (define (full? dct)
   (bst:full? dct)))